plugins {
    id 'java-project'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

configurations {
    dependingLibrary
}

dependencies {
    dependingLibrary ('com.jayway.jsonpath:json-path:2.5.0') {
        exclude module: 'json-smart'
    }
    dependingLibrary 'com.fasterxml.jackson.core:jackson-databind:2.12.3'
    dependingLibrary 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.12.3'

    testImplementation 'com.github.tomakehurst:wiremock-jre8:2.27.2'
    testImplementation 'org.assertj:assertj-core:3.19.0'
}

configurations.compileOnly.extendsFrom(configurations.dependingLibrary)
configurations.testImplementation.extendsFrom(configurations.dependingLibrary)
configurations.shadow.extendsFrom(configurations.dependingLibrary).exclude module: 'slf4j-api'

jar {
    enabled = false
    finalizedBy 'shadowJar'
}

test {
    environment('CONFIG_YAML', project.file('src/test/resources/http-spec-runner.yaml'))
}

shadowJar {
    relocate 'com.fasterxml.jackson', 'shadow.jackson'
    relocate 'com.jayway.jsonpath', 'shadow.jsonpath'
    exclude 'org/slf4j/**'
    exclude 'META-INF/maven/org.slf4j/**'
    finalizedBy 'finalizeShadowJar'
}

task finalizeShadowJar(type: Copy, group: 'build', description: 'renames shadowJar outputs') {
    from shadowJar.outputs
    into jar.destinationDirectory
    rename('\\-all', '')
}
